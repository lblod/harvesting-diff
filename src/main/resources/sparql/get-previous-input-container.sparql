PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX jobst: <http://redpencil.data.gift/id/concept/JobStatus/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX tasko: <http://lblod.data.gift/id/jobs/concept/TaskOperation/>
PREFIX dlstatus: <http://lblod.data.gift/file-download-statuses/>

SELECT DISTINCT ?path WHERE {

  ?job
    a cogs:Job ;
	  adms:status jobst:success .

  ?taskCollecting
    dct:isPartOf ?job ;
    task:operation tasko:collecting ;
    adms:status jobst:success ;
    task:resultsContainer ?dataContainer .

  ?dataContainer
    task:hasFile ?remoteDataObject .

  ?remoteDataObject
    nie:url ?targetUrl ;
    adms:status dlstatus:collected .

  FILTER (?targetUrl = <${targetUrl}>)

  # we only want to grab the previous jobs where data were published.
  ?taskPublishing
    dct:isPartOf ?job ;
    task:operation tasko:publishHarvestedTriples ;
    adms:status jobst:success .

  ?taskDiff
    dct:isPartOf ?job ;
    task:operation tasko:diff ;
    adms:status jobst:success ;
    task:inputContainer ?inputContainer .

  ?inputContainer
    task:hasFile ?file .

  ?path
    nie:dataSource ?file .

  ?remoteDataObject
    nie:url ?targetUrl .
}
